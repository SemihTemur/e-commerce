plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
}

group = 'com.semih'
version = '0.0.1-SNAPSHOT-plain'
description = 'common'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.named('jar') {
    enabled = true
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2025.0.0")
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.springframework.boot:spring-boot-starter-security'
    api 'org.springframework.boot:spring-boot-starter-actuator'
    api 'io.micrometer:micrometer-tracing-bridge-brave'
    api 'io.zipkin.reporter2:zipkin-reporter-brave'
    api 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'
    api 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    api 'org.springframework.boot:spring-boot-starter-validation'
    api 'org.springframework.boot:spring-boot-starter-test'
    api 'org.junit.platform:junit-platform-launcher'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'common'
            version = project.version
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.register("buildAndPublish") {
    group = "build"
    description = "Builds the plain JAR and publishes it to the local Maven repository"
    dependsOn("build", "publishToMavenLocal")
}
